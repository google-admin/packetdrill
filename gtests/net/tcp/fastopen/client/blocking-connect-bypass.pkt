//
// Blocking write() with TCP_FASTOPEN_CONNECT sockopt
//
`../../common/defaults.sh
 ../../common/set_sysctls.py /proc/sys/net/ipv4/tcp_timestamps=0`

// Cache warmup: send a Fast Open cookie request
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
   +0 setsockopt(3, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
   +0 connect(3, ..., ...) = -1 EINPROGRESS (Operation is now in progress)
   +0 > S 0:0(0) <mss 1460,nop,nop,sackOK,nop,wscale 8,FO,nop,nop>
 +.01 < S. 123:123(0) ack 1 win 5840 <mss 1460,nop,nop,sackOK,nop,wscale 6,FO abcd1234,nop,nop>
   +0 > . 1:1(0) ack 1
 +.01 close(3) = 0
   +0 > F. 1:1(0) ack 1
 +.01 < F. 1:1(0) ack 2 win 92
   +0 > .  2:2(0) ack 2


//
// Test: write(1000B) with TCP_FASTOPEN_CONNECT sockopt and connect()
//
 +.01 socket(..., SOCK_STREAM, IPPROTO_TCP) = 4
   +0 setsockopt(4, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
   +0 connect(4, ..., ...) = 0
 +.01...0.020 write(4, ..., 1000) = 1000
   +0 > S 0:1000(1000) <mss 1460,nop,nop,sackOK,nop,wscale 8,FO abcd1234,nop,nop>
 +.01 < S. 1234:1234(0) ack 1001 win 5840 <mss 940,nop,nop,sackOK,nop,wscale 6,FO 1234abcd5678dcba,nop,nop>
   +0 > . 1001:1001(0) ack 1
 +.01 < P. 1:501(500) ack 1000 win 257
   +0 > . 1001:1001(0) ack 501
 +.01 read(4, ..., 4096) = 500
   +0 close(4) = 0
   +0 > F. 1001:1001(0) ack 501
 +.02 < F. 501:501(0) ack 1002 win 257
   +0 > . 1002:1002(0) ack 502


//
// Test: sendto(1900B) with TCP_FASTOPEN_CONNECT sockopt and connect()
//
   +0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 5
   +0 setsockopt(5, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
   +0 connect(5, ..., ...) = 0
   +0...0.020 sendto(5, ..., 1900, 0, ..., ...) = 1900
// 940 - 40 (max tcp hdr) = 900
   +0 > S 0:900(900) <mss 1460,nop,nop,sackOK,nop,wscale 8,FO 1234abcd5678dcba,nop,nop>
 +.02 < S. 5678:5678(0) ack 901 win 5840 <mss 1460,nop,nop,sackOK,nop,wscale 6,FO abcd1234,nop,nop>
   +0 > P. 901:1901(1000) ack 1
 +.02 < P. 1:501(500) ack 1901 win 257
// Ack is delayed until FIN
   +0 read(5, ..., 4096) = 500
   +0 close(5) = 0
   +0 > F. 1901:1901(0) ack 501
 +.02 < F. 501:501(0) ack 1902 win 257
   +0 > . 1902:1902(0) ack 502

`/tmp/sysctl_restore_${PPID}.sh`
