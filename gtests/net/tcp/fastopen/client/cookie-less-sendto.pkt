//
// sendto(MSG_FASTOPEN) in cookie-less mode
// (sysctl net.ipv4.tcp_fastopen mask 4)
//
`../../common/defaults.sh
 ../../common/set_sysctls.py /proc/sys/net/ipv4/tcp_timestamps=0 \
                             /proc/sys/net/ipv4/tcp_fastopen=5`

//
// Test: blocking sendto(1000B, MSG_FASTOPEN)
//
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 4
 +.05...0.060 sendto(4, ..., 1000, MSG_FASTOPEN, ..., ...) = 1000
// Pure SYN-data, no cookie required
   +0 > S 0:1000(1000) <mss 1460,nop,nop,sackOK,nop,wscale 8>
 +.01 < S. 1234:1234(0) ack 1001 win 5840 <mss 940,nop,nop,sackOK,nop,wscale 6>
   +0 > . 1001:1001(0) ack 1
   +0 %{ assert (tcpi_options & TCPI_OPT_SYN_DATA) != 0, tcpi_options }%
 +.01 < P. 1:501(500) ack 1000 win 257
   +0 > . 1001:1001(0) ack 501
   +0 read(4, ..., 4096) = 500
   +0 close(4) = 0
   +0 > F. 1001:1001(0) ack 501
 +.02 < F. 501:501(0) ack 1002 win 257
   +0 > . 1002:1002(0) ack 502


//
// Test: blocking sendto(1900B, MSG_FASTOPEN)
//
 +.01 socket(..., SOCK_STREAM, IPPROTO_TCP) = 5
   +0...0.020 sendto(5, ..., 1900, MSG_FASTOPEN, ..., ...) = 1900
// 940 - 40 (max tcp hdr) = 900
   +0 > S 0:900(900) <mss 1460,nop,nop,sackOK,nop,wscale 8>
// Unsolicited cookies is not cached (see SYN at time 6.450)
 +.02 < S. 5678:5678(0) ack 901 win 5840 <mss 1460,nop,nop,sackOK,nop,wscale 6,FO abcd1234,nop,nop>
   +0 > P. 901:1901(1000) ack 1
   +0 %{ assert (tcpi_options & TCPI_OPT_SYN_DATA) != 0, tcpi_options }%
 +.02 < P. 1:501(500) ack 1901 win 257
   +0 read(5, ..., 4096) = 500
   +0 close(5) = 0
   +0 > F. 1901:1901(0) ack 501
 +.02 < F. 501:501(0) ack 1902 win 257
   +0 > . 1902:1902(0) ack 502

//
// Test: blocking sendto(1900B, MSG_FASTOPEN)
//
 +.01 socket(..., SOCK_STREAM, IPPROTO_TCP) = 5
   +0...0.020 sendto(5, ..., 5000, MSG_FASTOPEN, ..., ...) = 5000
   +0 > S 0:1420(1420) <mss 1460,nop,nop,sackOK,nop,wscale 8>
// Unsolicited cookies is not cached (see SYN at time 6.450)
 +.02 < S. 9999:9999(0) ack 1421 win 5840 <mss 1460,nop,nop,sackOK,nop,wscale 6,FO abcd1234,nop,nop>
   +0 > P. 1421:4341(2920) ack 1
   +0 %{ assert (tcpi_options & TCPI_OPT_SYN_DATA) != 0, tcpi_options }%
   +0 > P. 4341:5001(660) ack 1
 +.02 < P. 1:1(0) ack 5001 win 257
   +0 close(5) = 0
   +0 > F. 5001:5001(0) ack 1
 +.02 < F. 1:1(0) ack 5002 win 257
   +0 > . 5002:5002(0) ack 2

//
// Test: non-blocking sendto(1000B, MSG_FASTOPEN)
//
 +.02 socket(..., SOCK_STREAM, IPPROTO_TCP) = 4
   +0 sendto(4, ..., 1000, MSG_FASTOPEN|MSG_DONTWAIT, ..., ...) = 1000
   +0 > S 0:1000(1000) <mss 1460,nop,nop,sackOK,nop,wscale 8>
   +1 > S 0:0(0) <mss 1460,nop,nop,sackOK,nop,wscale 8>
 +.02 < S. 0:0(0) ack 1 win 5840 <mss 1460,nop,nop,sackOK,nop,wscale 6>
// Test: immediately retransmits data on server SYN/ACK
   +0 > P. 1:1001(1000) ack 1
   +0 %{ assert (tcpi_options & TCPI_OPT_SYN_DATA) == 0, tcpi_options }%
   +0 < P. 1:1401(1400) ack 1001 win 257
   +0 > . 1001:1001(0) ack 1401
   +0 read(4, ..., 4096) = 1400
   +0 close(4) = 0


//
// Test: unsolicited cookies in previous SYN-ACK are not cached
//
 +.03 `sysctl -q net.ipv4.tcp_fastopen=1`
 +.15 socket(..., SOCK_STREAM, IPPROTO_TCP) = 4
   +0 sendto(4, ..., 1000, MSG_FASTOPEN|MSG_DONTWAIT, ..., ...) = -1 EINPROGRESS (Operation now in progress)
   +0 > S 0:0(0) <mss 1460,nop,nop,sackOK,nop,wscale 8,FO,nop,nop>

`/tmp/sysctl_restore_${PPID}.sh`
