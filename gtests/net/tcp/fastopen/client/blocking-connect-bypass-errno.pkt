//
// Test TCP_FASTOPEN_CONNECT sockopt with blocking write() errno(s).
//
`../../common/defaults.sh
 ../../common/set_sysctls.py /proc/sys/net/ipv4/tcp_timestamps=0`

// Cache warmup: send a Fast Open cookie request
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
   +0 setsockopt(3, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
   +0 connect(3, ..., ...) = -1 EINPROGRESS (Operation is now in progress)
   +0 > S 0:0(0) <mss 1460,nop,nop,sackOK,nop,wscale 8,FO,nop,nop>
 +.01 < S. 123:123(0) ack 1 win 5840 <mss 1460,nop,nop,sackOK,nop,wscale 6,FO abcd1234,nop,nop>
   +0 > . 1:1(0) ack 1
 +.01 close(3) = 0
   +0 > F. 1:1(0) ack 1
 +.01 < F. 1:1(0) ack 2 win 92
   +0 > .  2:2(0) ack 2


//
// Test: ECONNREFUSED when remote resets on SYN
//
 +.17 socket(..., SOCK_STREAM, IPPROTO_TCP) = 4
   +0 setsockopt(4, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
   +0 connect(4, ..., ...) = 0
   +0...0.050 write(4, ..., 2000) = -1 ECONNREFUSED (Connection Refused)
   +0 > S 0:1420(1420) <mss 1460,nop,nop,sackOK,nop,wscale 8,FO abcd1234,nop,nop>
 +.05 < R. 0:0(0) ack 1 win 0
   +0 %{ assert (tcpi_options & TCPI_OPT_SYN_DATA) == 0, tcpi_options }%
   +0 close(4) = 0


//
// Test: ETIMEOUT when SYN timed out
//
 +.05 socket(..., SOCK_STREAM, IPPROTO_TCP) = 5
   +0 setsockopt(5, IPPROTO_TCP, TCP_SYNCNT, [1], 4) = 0
   +0 setsockopt(5, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
   +0 connect(5, ..., ...) = 0
   +0...3.000 write(5, ..., 2000) = -1 ETIMEDOUT (Connection Timed Out)
   +0 > S 0:1420(1420) <mss 1460,nop,nop,sackOK,nop,wscale 8,FO abcd1234,nop,nop>
   +1 > S 0:0(0) <mss 1460,nop,nop,sackOK,nop,wscale 8>
+2.005 %{ assert (tcpi_options & TCPI_OPT_SYN_DATA) == 0, tcpi_options }%
+.695 close(5) = 0

`/tmp/sysctl_restore_${PPID}.sh`
